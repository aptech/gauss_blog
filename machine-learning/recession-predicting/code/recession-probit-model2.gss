new;
library cmlmt, dc, gml;

/*
** Load data from url
*/
url = "https://github.com/aptech/gauss_blog/blob/master/machine-learning/recession-predicting/data/final_data.gdat?raw=true";
reg_data = loadd(url);

/*
** Split data
*/
y = reg_data[., "USREC"];
reg_x = reg_data[., 3:cols(reg_data)];

// Split data into (60%) training and (40%) test sets
shuffle = "False";
{ y_train, y_test, x_train, x_test } = trainTestSplit(y, reg_x, 0.6, shuffle);
{ date_train, date_test, tmp, tmp2 } = trainTestSplit(reg_data[., "date"], reg_x, 0.6, shuffle);

/*
** Train  model
*/
// Assign starting values for estimation
beta_strt = 0.5*ones(cols(reg_x), 1);

// Declare 'out' to be a cmlmtResults structure
// to hold the results of the estimation
struct cmlmtResults cout;
 
// Perform estimation and print results
cout = cmlmt(&probit, beta_strt, y_train, x_train);
call cmlmtPrt(cout);

/*
** Predictions
*/
beta_hat = pvUnpack(cout.par, "x");

{ y_prob, y_hat } = probitFit(beta_hat, x_test, y_test);

{ y_prob2, y_hat2 } = probitFit(beta_hat, x_test, y_test, 0.75);

/*
** Set up graph
*/
struct plotControl pctl;
pctl = plotgetDefaults("xy");

// Set up title
plotSetTitle(&pctl, "Probability of recession based on probit model.", "Arial", 18);

// Plot original data
plotOpenWindow();
plotXY(pctl, date_test, y_prob);

// Add markers
struct plotControl pctl2;
pctl2 = plotGetDefaults("xy");

// Set line color
plotSetLinePen(&pctl2, 2, "gray", 3);

// Plot 50% line
plotAddHLine(pctl2, 0.5);
plotAddHLine(pctl2, 0.75);

/*
** Add recession bars
*/

// Reset plotControl to default bar settings
struct plotControl plt;
plt = plotGetDefaults("bar");

// Bar fill: solid, 15% opaque and gray
plotSetFill(&plt, 1, 0.15, "gray");

// Add legend item
plotSetLegend(&plt, "Recession", "top left inside");

// Add vertical bars to any location where
// the second column of 'usrec' contains a 1
plotAddVBar(plt, reg_data[., "date" "USREC"]);

/*
** Likelihood Function
*/
proc (1) = probit(beta_, y, X, ind);
    local mu;
 
    // Declare 'mm' to be a modelResults
    // structure to hold the function value
    struct modelResults mm;

    // Compute mu
    mu = X * beta_;
 
    // Assign the log-likelihood value to the
    // 'function' member of the modelResults structure
    mm.function = y.*lncdfn(mu) + (1-y).*lncdfnc(mu);
 
    // Return the model results structure
    retp(mm);
endp;

proc (2) = probitFit(beta_hat, x_test, y_test, ...);
    local prob, y_prob, y_hat, fmt;
    
    prob = dynargsGet(1, 0.5);
    
    // Predicted probability of recession 
    // outcome
    y_prob = cdfn(x_test * beta_hat);
    
    // Classify data as recession or non-recession
    y_hat = where(y_prob .>= prob, 1, 0);
    
    // Print out model quality evaluation statistics
    fmt = "Probit model with %d%% cutoff.";
    sprintf(fmt, 100*prob);
    call binaryClassMetrics(y_test, y_hat);

    retp(y_prob, y_hat);
endp;
